dnl Process this file with autoconf to produce a configure script.

dnl @(#)$Id: configure.in,v 2.9 2009/06/25 04:13:48 baccala Exp $

dnl Copyright (C) 1996 - 2001 Tim Witham <twitham@quiknet.com>

dnl (see the files README and COPYING for more details)

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(xoscope, 2.0)
AM_CONFIG_HEADER(config.h)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

PKG_CHECK_MODULES(GTK, gtk+-2.0 >= 2.2)

#PKG_CHECK_MODULES(GTKDATABOX, gtkdatabox)
#AC_CONFIG_SUBDIRS(gtkdatabox-0.6.0.0)

dnl Set PACKAGE_DATA_DIR in config.h.
if test "x${datadir}" = 'x${prefix}/share'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${ac_default_prefix}/share/${PACKAGE}")
  else
    AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share/${PACKAGE}")
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${datadir}/${PACKAGE}")
fi

dnl Set PACKAGE_LIBEXEC_DIR in config.h.
if test "x${libexecdir}" = 'x${exec_prefix}/libexec'; then
  if test "x${prefix}" = "xNONE"; then
    AC_DEFINE_UNQUOTED(PACKAGE_LIBEXEC_DIR, "${ac_default_prefix}/libexec/${PACKAGE}")
  else
    AC_DEFINE_UNQUOTED(PACKAGE_LIBEXEC_DIR, "${exec_prefix}/libexec/${PACKAGE}")
  fi
else
  AC_DEFINE_UNQUOTED(PACKAGE_LIBEXEC_DIR, "${libexecdir}/${PACKAGE}")
fi

dnl Set PACKAGE_SOURCE_DIR in config.h.
packagesrcdir=`cd $srcdir && pwd`
AC_DEFINE_UNQUOTED(PACKAGE_SOURCE_DIR, "${packagesrcdir}")

dnl Use -Wall if we have gcc.
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

dnl X11 checks
AC_PATH_XTRA
if test "-DX_DISPLAY_MISSING" = "$X_CFLAGS"; then
  AC_MSG_ERROR(can not find X11)
fi
AC_SUBST(X_CFLAGS)
AC_SUBST(X_PRE_LIBS)
AC_SUBST(X_EXTRA_LIBS)
AC_SUBST(X_LIBS)
AC_SUBST(x_libraries)

dnl Checks for programs.
AC_PATH_PROG(PERL, perl, /usr/bin/perl)
AC_PATH_PROG(RANLIB, ranlib)

dnl Checks for libraries.
AC_CHECK_LIB(esd, esd_monitor_stream)
AC_CHECK_LIB(m, sin)

dnl This, believe it or not, is the suggested syntax for checking the
dnl result of a previously run test - in our case, for setting an
dnl automake variable based on the result of an autoconf test.  See
dnl "Defining C Preprocessor Symbols" in the autoconf info docs.

AC_CHECK_LIB(comedi, comedi_open)
AM_CONDITIONAL(COMEDI, test "$ac_cv_lib_comedi_comedi_open" = "yes")

AC_SUBST(XOSCOPE)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h limits.h sys/ioctl.h sys/time.h termio.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_HEADER_MAJOR
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(getcwd putenv select strdup strstr strtol)

AC_OUTPUT([
operl
Makefile
gtkdatabox-0.6.0.0/gtk/Makefile
])
